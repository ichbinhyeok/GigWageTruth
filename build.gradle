plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.1'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'gg.jte.gradle' version '3.1.12'
}

group = 'com.gigwager'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
        // vendor = JvmVendorSpec.ADOPTIUM  // Removed for Docker compatibility (Liberica)
    }
}

tasks.withType(JavaCompile) {
    options.compilerArgs << "-parameters"
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'gg.jte:jte:3.1.12'
    implementation 'gg.jte:jte-spring-boot-starter-3:3.1.12'
    implementation 'org.jsoup:jsoup:1.18.3'
    
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'com.microsoft.playwright:playwright:1.52.0'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

jte {
    generate()
    binaryStaticContent = true
}

// Ensure generated JTE sources are included in the compilation
sourceSets {
    main {
        java {
            srcDirs += 'build/generated-sources/jte'
        }
    }
}

// Ensure JTE generation happens before Java compilation
tasks.named('compileJava') {
    dependsOn tasks.named('generateJte')
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.register('installPlaywrightChromium', JavaExec) {
    group = 'verification'
    description = 'Install Playwright Chromium for e2e browser tests'
    classpath = sourceSets.test.runtimeClasspath
    mainClass = 'com.microsoft.playwright.CLI'
    args 'install', 'chromium'
}

// Enable JTE development mode only for local bootRun
tasks.named('bootRun') {
    systemProperty 'gg.jte.developmentMode', 'true'
    systemProperty 'gg.jte.usePrecompiledTemplates', 'false'
}

def sitemapTrackedPaths = [
        'src/main/jte',
        'src/main/resources/data',
        'src/main/java/com/gigwager/controller',
        'src/main/java/com/gigwager/service',
        'src/main/java/com/gigwager/model/content'
]

tasks.register('generateSitemapLastmod') {
    group = 'build setup'
    description = 'Generates sitemap-lastmod.txt from the latest git commit touching SEO/content sources.'
    outputs.file("$buildDir/generated-resources/sitemap-lastmod.txt")

    doLast {
        def lastmod = java.time.LocalDate.now().toString()
        def out = new ByteArrayOutputStream()
        def err = new ByteArrayOutputStream()

        try {
            exec {
                commandLine(['git', 'log', '-1', '--format=%cs', '--'] + sitemapTrackedPaths)
                standardOutput = out
                errorOutput = err
                ignoreExitValue = true
            }

            def gitDate = out.toString().trim()
            if (gitDate ==~ /\d{4}-\d{2}-\d{2}/) {
                lastmod = gitDate
            }
        } catch (Exception ignored) {
            // Fall back to current date when git metadata is unavailable in the runtime.
        }

        def outputFile = file("$buildDir/generated-resources/sitemap-lastmod.txt")
        outputFile.parentFile.mkdirs()
        outputFile.text = lastmod + System.lineSeparator()
    }
}

sourceSets {
    main {
        resources {
            srcDir("$buildDir/generated-resources")
        }
    }
}

tasks.named('processResources') {
    dependsOn tasks.named('generateSitemapLastmod')
}
